
apply plugin: org.standardout.gradle.plugin.platform.PlatformPlugin

defaultTasks 'clean', 'updateSite'

repositories {
	mavenLocal()
	mavenCentral()
	maven {
		url "http://repo.aduna-software.org/maven2/releases"
	}
	maven {
		url "http://fortytwo.net/maven2"
	}
	// ivy {   url "http://repo.mycompany.com/repo"}
}

project.ext {
	// project specific variables
	groovyVersion = '2.1.5'
	slf4jVersion = '1.7.5'
	schemacrawlerVersion = '8.16'
}

configurations {
	platform {
		// exclude log4j (we provide it over slf4j)
		exclude group: 'log4j'
		
		resolutionStrategy.eachDependency { DependencyResolveDetails details ->
			if (details.requested.name == 'commons-logging') {
				// prefer 'jcl-over-slf4j' over 'commons-logging'
				details.useTarget "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
			}
		}
	}
}

platform {
	// platform extension - plugin specific configuration
	
	// fetch sources for dependencies if possible (result in source bundles)
	fetchSources = true
	
	// dependencies can be declared in the extension
	// (in fact, they need to be, if a custom bnd configuration is needed)
	
	// database
	bundle "net.sourceforge.schemacrawler:schemacrawler:$schemacrawlerVersion", {
		bnd {
			// custom bnd configuration
			instruction 'Import-Package', 'freemarker.*;resolution:=optional,*' // freemarker is optional
			// export schemacrawler only
			instruction 'Export-Package', """
					schemacrawler;version=$schemacrawlerVersion,
					schemacrawler.*;version=$schemacrawlerVersion"""
			instruction 'Private-Package', '*' // other packages private
		}
	}
	bundle 'com.thoughtworks.xstream:xstream:1.4.5' // dependency of schemacrawler not listed in pom!
	
	// security
	bundle 'org.jasig.cas:cas-client:3.1.10'
}

dependencies {
	// platform dependencies defined w/o bundle customization
	
	// groovy (use groovy-all)
	platform "org.codehaus.groovy:groovy-all:${groovyVersion}"
	
	platform 'org.codehaus.groovy.modules.http-builder:http-builder:0.6', {
		// exclude groovy dependency
		exclude group: 'org.codehaus.groovy', module: 'groovy'
	}
	
	// logging
	platform "org.slf4j:slf4j-api:${slf4jVersion}"
	platform "org.slf4j:log4j-over-slf4j:${slf4jVersion}"
	platform "org.slf4j:jul-to-slf4j:${slf4jVersion}"
	platform "org.slf4j:slf4j-ext:${slf4jVersion}"
	platform 'ch.qos.logback:logback-classic:1.0.13'
	platform 'ch.qos.logback:logback-core:1.0.13'
	
	// utilities
	platform 'com.google.guava:guava:16.0.1'
	platform 'net.sf.trove4j:trove4j:3.0.3'
	platform 'commons-io:commons-io:2.4'
	platform 'net.sf.ehcache:ehcache-core:2.6.6'
	platform group: 'org.pegdown', name: 'pegdown', version: '1.4.2'
	
	// XML
	platform 'org.apache.ws.xmlschema:xmlschema-core:2.0.2'
//	platform 'org.apache.ws.commons.schema:XmlSchema:1.4.7'
	
	// jackson 1
	platform 'org.codehaus.jackson:jackson-core-asl:1.9.13'
	// jackson 2
	platform 'com.fasterxml.jackson.core:jackson-core:2.3.0'
	platform 'com.fasterxml.jackson.core:jackson-databind:2.3.0'
	
	// local dependencies
	//TODO not yet supported
	platform files('libs/groovy-sandbox-1.6-SNAPSHOT.jar')
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}